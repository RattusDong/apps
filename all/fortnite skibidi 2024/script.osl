save_access = system_url.contains("embed=").not()

if save_access (
  save "fortniteskibidi@rattusdong" "set_directory"

  if "highscore.txt".saveExists() (
    highscore = "highscore.txt".saveGet()
  ) else (
    save "highscore.txt" "set" 0
    highscore = 0
  )
) else (
  highscore = 0
)

assets = "https://raw.githubusercontent.com/RattusDong/apps/main/all/fortnite%20skibidi%202024/assets/"

def shootgun() (
  turnleft 45
  shotx = window.left + (180 + (90 * direction.sin()) * scale)
  shoty = 90 * direction.cos() * scale
  shotd = direction + 180
  
  sound "shoot" "start" 0
  if contr (
    void gamepads[1].haptic({
      duration: 300,
      weakMagnitude: 0.0,
      strongMagnitude: 0.5
    })
  )
  ready = false
  cooldown = 20
)

sound assets ++ "shotgun.mp3" "load" "shoot"
sound "shoot" "volume" 0.8
sound assets ++ "hit.mp3" "load" "kill"
sound "kill" "volume" 1.0
window.show()
window.resize(800,450)

shotTimer = 0

skibX = 1000 + random(200,450)
skibY = random(-1000,1000)
speed = 2
score = 0
dead = false
dieOff = 0
click = flase
sgwidth = 0
cooldown = 0
ready = false
shotx = 3000
shoty = 3000
shotd = 90
updown = 0
contr = false

mainloop:
array gamepads = getGamepads()
if gamepads.len > 0 (
  gamepad @= gamepads[1]
  contr = true
)

local bg_dim = [1600, 900]
local w = window.width
local h = window.height
if w / 1600 < h / 900 (
  scale = h / 900 * 2
  w = null
) else (
  scale = w / 1600 * 2
  h = null
)
if window.height > window.width / 1.5 (
  scale /= 2
)
goto 0 0
image assets ++ "background.png" w h
goto window.left + (100 * scale) -100 * scale + dieOff
image assets ++ "jonsey.png" 500 * scale

goto skibX / 1000 * window.right skibY / 1200 * window.top
image assets ++ "skibidi.png" 80 * scale 120 * scale

if dist(shotx, shoty, x_position, y_position) < 80 * scale and x_position < window.right (
  sound "kill" "start" 0
  skibX = 1000 + random(200,450)
  speed += 0.4
  score ++
  if contr ( 
    void gamepads[1].haptic({
      duration: 400,
      weakMagnitude: 0.9,
      strongMagnitude: 0.2
    })
  )
  if score > highscore (
    if save_access (
      save "highscore.txt" "set" score
    )
    highscore = score
  )
  skibY = random(-1000,1000)
)
if skibX < -200 (
  dead = true
)

goto 0 window.top - 30
centext "Skibidi Kills: " ++ score 20 : c#fff
change_y -30
centext "Highest: " ++ highscore 16

direction shotd
goto shotx shoty
effect "transparency" 0 + (cooldown - 16) * 25
image assets ++ "shotgunproj.png" 10 + (dist(shotx, shoty, -250, 0) * 0.35)

shotx += -80 * direction.sin()
shoty += -80 * direction.cos()

effect "transparency" 0

goto window.left + (180 * scale) 0 + dieOff

if contr (
  stickX = gamepads[1].axes[1].x
  stickY = gamepads[1].axes[1].y
  if stickX.abs() < 0.15 and stickY.abs() < 0.15 (
    stickX = slx
    stickY = sly
  )
  goto 0 0
  pointat stickX stickY
  goto -230 0 + dieOff
  slx = stickX
  sly = stickY
) else (
  pointat mouse_x mouse_y
)
turnleft 90
direction direction.clamp(-60,60) + (-10 + ((cooldown - 16) * -1)).clamp(-14,0)
turnright 135
image assets ++ "shotgun.png" 180 - sgwidth * scale
if cooldown > 8 (
  effect "transparency" 0 + (cooldown - 14) * -16
  image assets ++ "shotgunshoot.png" 310 320
)
effect "transparency" 0
if mouse_down or gamepad.buttons[8]["pressed"] and cooldown <= 0 (
  sgwidth = (sgwidth + 1).clamp(0,10)
  if sgwidth == 8 (
    if contr (
      void gamepads[1].haptic({
        duration: 600,
        weakMagnitude: 0.05,
        strongMagnitude: 0.0
      })
    )
    ready = true
  )
) else (
  sgwidth = 0
)
if contr (
  if gamepad.buttons[8]["pressed"].not() and ready and dead.not() (
    shootgun()
  )
) else if mouse_down.not() and ready and dead.not() (
  shootgun()
)

cooldown -= 1

direction 90
if dead (
  goto 0 -150
  image assets ++ "fire.png" 800 * scale 500 * scale
 
  goto 0 0
  if contr (
    centext "Press A to restart" 15
  
    void gamepads[1].haptic({
      duration: 200,
      weakMagnitude: 1.0,
      strongMagnitude: 1.0
    })
  ) else (
    centext "Press Space to restart" 15
  )

  dieOff = 2000

  if "space".isKeyDown() or gamepad.buttons[1]["pressed"] or gamepad.buttons[2]["pressed"] (
    dead = false
    dieOff = 0
    score = 0
    skibX = 1000 + random(200,450)
    skibY = random(-200,200)
    speed = 1
    ready = false
    cooldown = 0
  )
)

skibX -= speed

loc -2 2 -20 -20
square 20 20 10 : c#222
icon "w 4 line 10 10 -10 -10 line -10 10 10 -10" 0.7 : c#fff
if clicked (
  window.close()
)

// special thanks to rattus

// updated by mist to replace old syntax with more modern equivalents
// updated by mist to make it resizable